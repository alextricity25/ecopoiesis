#!ipxe
# CentOS Generic Installer

:validate_os
iseq ${ecopoiesis_boot_os_version} 7 && goto validate_status ||
goto reload_ecopoiesis

:validate_status
iseq ${ecopoiesis_operational_status} production && goto localboot ||
iseq ${ecopoiesis_boot_status} install_os && goto install_os ||
iseq ${ecopoiesis_operational_status} provisioning && goto localboot ||
iseq ${ecopoiesis_boot_status} localboot && goto localboot ||
goto reload_ecopoiesis

:reload_ecopoiesis
chain --autofree --replace http://${ecopoiesis_host}/ecopoiesis.ipxe
exit

:localboot
echo Booting locally...
sanboot --no-describe --drive 0x80 ||
exit
goto validate_os

:install_os
set mirror mirror.rackspace.com
set dir centos/${ecopoiesis_boot_os_version}
set netcfg ip=${mgmt_ip_address}::${mgmt_gateway_ip}:${mgmt_netmask}:${hostname}::none nameserver=${primary_dns}
kernel http://${mirror}/${dir}/os/x86_64/images/pxeboot/vmlinuz repo=http://${mirror}/${dir} ${netcfg} ks=http://${ecopoiesis_host}/plip/pxe/configs/${ecopoiesis_boot_os}-${ecopoiesis_boot_os_version}-${ecopoiesis_boot_profile}?number=${host_id} ${console}
initrd http://${mirror}/${dir}/os/x86_64/images/pxeboot/initrd.img
boot
goto validate_os
